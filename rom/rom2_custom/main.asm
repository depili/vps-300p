ORG 0x0

START:
	DI
	IM	2
	LD	A,00h
	LD	I,A
	LD	SP,0F7FFh	; Stack
	CALL	INIT_F4_F0_F1
	JR	INIT
; Second init here at 0x0012

	; L001C
INIT:
	CALL	INIT_EI_RETI
	CALL	INIT_IO
	CALL	TX_INIT
	EI
	TX_A	"X"
	JP	MAIN_LOOP


	SEEK 0x70
	ORG 0x70
	; Interrupts
INTERRUPT_VECTORS:
	DW	INT_SIO_B_TX_EMPTY
	DW	INT_SIO_B_STATUS_CHANGE
	DW	INT_SIO_B_RX_AVAILABLE
	DW	INT_SIO_B_ERROR
	DW	INT_SIO_A_TX_EMPTY
	DW	INT_SIO_A_STATUS_CHANGE
	DW	INT_SIO_A_RX_AVAILABLE
	DW	INT_SIO_A_ERROR
CTC_VECTORS:
	DW	INT_CTC_CH1
	DW	INT_CTC_CH2
	DW	INT_CTC_CH3
	DW	INT_CTC_CH4
PIO_VECTORS:
	DW	INT_PIO_A
	DW	INT_PIO_B

	SEEK 0x90
	ORG 0x90


	; L008C
MAIN_LOOP: PROC
	; 7 Segment display test
	LD	IX, KEYB_1_DISP
	LD	(IX), SEG_0
	LD	(IX+1), SEG_1
	LD	(IX+2), SEG_2
	LD	(IX+3), SEG_3
	LD	(IX+4), SEG_4
	LD	(IX+5), SEG_5
	LD	(IX+6), SEG_6
	LD	(IX+7), SEG_7
	CALL	KEYB_WRITE_1	; Write the LCD characters
	TX_A	"M"
	LD	A, 0x00
	LD	(ADC_READ_COUNTER), A
loop:
	CALL	KEYB_READ_1
	CALL	KEYB_READ_2
	CALL	KEYB_READ_3

	LD	HL, ADC_READ_COUNTER
	INC	(HL)
	JR	NZ, loop
	CALL	ADC_READ_ALL	; ADC reads
	LD	A, 0x80
	LD	(ADC_READ_COUNTER), A
	; LD	A, (ADC_3_DEST+1)
	; CALL SIO_A_TX_BLOCKING
	; LD	A, (ADC_3_DEST)
	; CALL SIO_A_TX_BLOCKING
	; TX_A	" "
	; TX_A	"-"
	JR	loop
ENDP

	; --- START PROC L0186 ---
INIT_IO:
L0186:	CALL	INIT_CTC
	CALL	INIT_SIO
	CALL	INIT_PIO
	CALL	INIT_KEYB
	CALL	PIO_A_SET_BIT_0
	CALL	INIT_ADC
	CALL	INIT_SHARED_MEM	; Zero shared memory
	RET


	; Zero and initialize various structures in shared memory
INIT_SHARED_MEM:
	ZFILL	0xF800, 0x07FF			; Zero the 2Kb shared memory window

	; Init LCD shared memory
	LD	HL, LCD_DEST
	LD	(LCD_POINTER), HL
	RET

	; L0176
INIT_EI_RETI:
	LD	A,0FBh
	LD	(8000h),A
	LD	A,0EDh
	LD	(8001h),A
	LD	A,4Dh
	LD	(8002h),A
	RET


INIT_F4_F0_F1:
	LD	A,00h
	OUT	(0F4h),A
	LD	A,5Bh
	OUT	(0F0h),A
	LD	A,0B1h
	OUT	(0F1h),A
	RET
